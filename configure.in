dnl configure.in for Openbox
dnl Initialize autoconf and automake
AC_INIT(src/blackbox.cc)
AM_INIT_AUTOMAKE(openbox,2.0.0,no-define)

dnl Determine default prefix
test x$prefix = "xNONE" && prefix="$ac_default_prefix"

dnl Check for various flavors of UNIX(r)
dnl AC_AIX
dnl AC_ISC_POSIX

dnl Locate required external software
AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL

AC_CHECK_PROGS(regex_cmd, sed)
if test x$regex_cmd = "x"; then
  AC_MSG_ERROR([error. sed is required to build the default menu file.])
fi

dnl Check for system header files
AC_CHECK_HEADERS(ctype.h dirent.h fcntl.h libgen.h locale.h nl_types.h process.h signal.h stdarg.h stdio.h string.h stdlib.h time.h unistd.h sys/param.h sys/select.h sys/signal.h sys/stat.h sys/time.h sys/types.h sys/wait.h)
AC_HEADER_TIME

dnl Check for existance of basename(), setlocale() and strftime()
AC_CHECK_FUNCS(basename, , AC_CHECK_LIB(gen, basename,
			  AC_DEFINE(HAVE_BASENAME) LIBS="$LIBS -lgen"))
AC_CHECK_FUNCS(getpid setlocale sigaction strftime strcasestr snprintf vsnprintf catopen catgets catclose)
AC_CHECK_LIB(nsl, t_open, LIBS="$LIBS -lnsl")
AC_CHECK_LIB(socket, socket, LIBS="$LIBS -lsocket")

dnl Check for X headers and libraries
AC_PATH_X
AC_PATH_XTRA

test x$no_x = "xyes" && AC_MSG_ERROR([Openbox requires the X Window System libraries and headers.])

test x$x_includes = "x" && x_includes="/usr/include"
test x$x_libraries = "x" && x_libraries="/usr/lib"

CFLAGS="$CFLAGS $X_CFLAGS"
CXXFLAGS="$CXXFLAGS $X_CFLAGS"
LIBS="$LIBS $X_LIBS"
LDFLAGS="$LDFLAGS $LIBS $X_PRE_LIBS"

dnl Check for required functions in -lX11
AC_CHECK_LIB(X11, XOpenDisplay,
  LIBS="$LIBS -lX11",
  AC_MSG_ERROR([Could not find XOpenDisplay in -lX11.])
)

LIBS="$LIBS $X_EXTRA_LIBS"

dnl Check for XShape extension support and proper library files.
SHAPE=""
AC_MSG_CHECKING([whether to build support for the XShape extension])
AC_ARG_ENABLE(
  shape, [  --enable-shape          enable support of the XShape extension [default=yes]])

: ${enableval="yes"}
if test x$enableval = "xyes"; then
  AC_MSG_RESULT([yes])
  AC_CHECK_LIB(Xext, XShapeCombineShape,
	AC_MSG_CHECKING([for X11/extensions/shape.h])
	AC_TRY_LINK(
#include <X11/Xlib.h>
#include <X11/Xutil.h>
#include <X11/extensions/shape.h>
, long foo = ShapeSet,
	  AC_MSG_RESULT([yes])
	  SHAPE="yes",
	  AC_MSG_RESULT([no])
	)
  )
else
  AC_MSG_RESULT([no])
fi
if test x$SHAPE = "xyes"; then
  LIBS="$LIBS -lXext"
  AC_DEFINE(SHAPE,1,Enable support of the XShape extension)
fi

dnl Check for Xft extension support and proper library files.
XFT=""
AC_MSG_CHECKING([whether to build support for the Xft extension])
AC_ARG_ENABLE(
  xft, [  --enable-xft            enable support of the Xft extension [default=no]],
  if test x$enableval = "xyes"; then
	AC_MSG_RESULT([yes])

	AC_CHECK_LIB(Xft, XftFontOpenXlfd,
	  AC_MSG_CHECKING([for X11/Xft/Xft.h])
	  AC_TRY_LINK(
#include <X11/Xlib.h>
#include <X11/Xft/Xft.h>
, XftFont foo,
        AC_MSG_RESULT([yes])
	    XFT="yes",
        AC_MSG_RESULT([no])
	  )
    )

  else
	AC_MSG_RESULT([no])
  fi,
  AC_MSG_RESULT([no])
)
if test x$XFT = "xyes"; then
  AC_DEFINE(XFT,1,Enable support of the Xft extension)
fi

dnl Check for the Slit
SLIT=""
AC_MSG_CHECKING([whether to include the Slit])
AC_ARG_ENABLE(
  slit, [  --enable-slit           include code for the Slit [default=yes]],
  if test x$enableval = "xyes"; then
    AC_MSG_RESULT([yes])
    SLIT="yes"
  else
    AC_MSG_RESULT([no])
  fi,
  AC_MSG_RESULT([yes])
  SLIT="yes"
)
if test x$SLIT = "xyes"; then
  AC_DEFINE(SLIT,1,Enable the Slit)
fi

dnl Check for ordered 8bpp dithering
ORDEREDPSEUDO=""
AC_MSG_CHECKING([whether to include Pseudocolor ordered dithering code])
AC_ARG_ENABLE(ordered-pseudo,
[  --enable-ordered-pseudo include code for ordered pseudocolor (8bpp)
                          dithering [default=no]],
  if test x$enableval = "xyes"; then
    AC_MSG_RESULT([yes])
    ORDEREDPSEUDO="yes" 
  else
    AC_MSG_RESULT([no]) 
  fi,
  AC_MSG_RESULT([no])
)
if test x$ORDEREDPSEUDO = "xyes"; then
  AC_DEFINE(ORDEREDPSEUDO,1,Enable pseudocolor ordered dithering)
fi

dnl Check for event clobbering
CLOBBER=""
AC_MSG_CHECKING([whether to intercept mouse events to clients])
AC_ARG_ENABLE(clobber,
[  --enable-clobber        intercept mouse events to clients when num lock 
			  or scroll lock are on [default=yes]],
  if test x$enableval = "xno"; then
    AC_MSG_RESULT([no])
    CLOBBER="no"
  else
    AC_MSG_RESULT([yes]) 
  fi,
  AC_MSG_RESULT([yes])
)
if test x$CLOBBER = "xno"; then
  AC_DEFINE(NOCLOBBER,1,Disable grabbing of mouse events when num/scoll lock is pressed)
fi

dnl Check whether to include debugging code
DEBUG=""
AC_MSG_CHECKING([whether to include verbose debugging code])
AC_ARG_ENABLE(debug,
  [  --enable-debug          include verbose debugging code [default=no]],
  if test x$enableval = "xyes"; then
    AC_MSG_RESULT([yes])
    DEBUG="-DDEBUG -Wall -W"
  else
    AC_MSG_RESULT([no])
    DEBUG="-DNDEBUG"
  fi,
  AC_MSG_RESULT([no])
  DEBUG="-DNDEBUG"
)
AC_SUBST(DEBUG)

dnl Check whether to include natural language support (i18n)
NLS=""
AC_MSG_CHECKING([whether to include NLS support])
AC_ARG_ENABLE(nls,
  [  --enable-nls            include natural language support [default=yes]],
  if test x$enableval = "xyes"; then
    AC_MSG_RESULT([yes])
    NLS="yes"
  else
    AC_MSG_RESULT([no])
  fi,
  AC_MSG_RESULT([yes])
  NLS="yes"
)

AC_CHECK_LIB(xpg4, setlocale, LIBS="$LIBS -lxpg4")

AC_CHECK_PROGS(gencat_cmd, gencat)
if test x$gencat_cmd = "x"; then
  NLS=""
fi

if test x$NLS = "xyes"; then
  AC_DEFINE(NLS,1,Include natural language support)
  AC_SUBST(NLS)
fi

dnl Check for new timed pixmap cache
TIMEDCACHE=""
AC_MSG_CHECKING([whether to use the timed pixmap cache])
AC_ARG_ENABLE(
  timed-cache,
[  --enable-timed-cache    use timed pixmap cache [default=yes]],
  if test x$enableval = "xyes"; then
    AC_MSG_RESULT([yes])
    TIMEDCACHE="yes"
  else
    AC_MSG_RESULT([no])
  fi,
  AC_MSG_RESULT([yes])
  TIMEDCACHE="yes"
)
if test x$TIMEDCACHE = "xyes"; then
  AC_DEFINE(TIMEDCACHE,1,Use timed pixmap cache)
fi

dnl Determine the return type of signal handlers
AC_TYPE_SIGNAL

dnl Determine if maintainer portions of the Makefiles should be included.
AM_MAINTAINER_MODE

dnl Print results
AC_MSG_RESULT([])
AC_MSG_RESULT([	$PACKAGE version $VERSION configured successfully.])
AC_MSG_RESULT([])
AC_MSG_RESULT([Using '$prefix' for installation.])
AC_MSG_RESULT([Using '$CXX' for C++ compiler.])
AC_MSG_RESULT([Building with '$CXXFLAGS' for C++ compiler flags.])
AC_MSG_RESULT([Building with '$LIBS' for linker flags.])
AC_MSG_RESULT([])

dnl Output files
AM_CONFIG_HEADER(config.h)
AC_OUTPUT(Makefile
src/Makefile
util/Makefile
data/Makefile
data/styles/Makefile
doc/Makefile
nls/Makefile
nls/C/Makefile
nls/da_DK/Makefile
nls/de_DE/Makefile
nls/es_ES/Makefile
nls/et_EE/Makefile
nls/fr_FR/Makefile
nls/hu_HU/Makefile
nls/it_IT/Makefile
nls/ja_JP/Makefile
nls/nl_NL/Makefile
nls/pt_BR/Makefile
nls/ru_RU/Makefile
nls/sl_SI/Makefile
nls/sv_SE/Makefile
nls/tr_TR/Makefile
nls/uk_UA/Makefile
nls/zh_CN/Makefile
nls/zh_TW/Makefile
version.h)
